allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
    }

    group 'com.github.bancolombia'

    sonar {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.projectKey", "bancolombia_secrets-manager"
            property "sonar.organization", "grupo-bancolombia"
            property "sonar.host.url", "https://sonarcloud.io/"
            property "sonar.sources", "src/main"
            property "sonar.test", "src/test"
            property "sonar.java.binaries", "build/classes"
            property "sonar.junit.reportPaths", "build/test-results/test"
            property "sonar.java-coveragePlugin", "jacoco"
            property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/generateMergedReport/generateMergedReport.xml"
            property "sonar.exclusions", ".github/**"
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    ext {
        pomConfig = {
            licenses {
                license {
                    name "MIT License"
                    url "https://opensource.org/licenses/mit-license.php"
                    distribution "repo"
                }
            }
            developers {
                developer {
                    id "dericop"
                    name "Daniel Rico Posada"
                    email "danielrico.posada@gmail.com"
                }
                developer {
                    id "alejobtc"
                    name "Alejandro Betancur Barrientos"
                    email "alejobtc@gmail.com"
                }
                developer {
                    id "andmagom"
                    name "Andres Mauricio Gomez P"
                    email "andmagom@outlook.com"
                }
            }

            scm {
                url "git@github.com:bancolombia/secrets-manager.git"
            }
        }
        groupId = 'com.github.bancolombia'
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.38'
        annotationProcessor 'org.projectlombok:lombok:1.18.38'

        testCompileOnly 'org.projectlombok:lombok:1.18.38'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
        testImplementation platform('org.junit:junit-bom:5.13.0-M3')
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.18.0'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)

    jacocoTestReport {
        dependsOn test
        reports {
            xml.setRequired true
            xml.setOutputLocation layout.buildDirectory.file("reports/jacoco.xml")
            csv.setRequired false
            html.setOutputLocation layout.buildDirectory.dir("reports/jacocoHtml")
        }
    }

    //    check.dependsOn dependencyCheckAggregate

    dependencyCheck {
        format = 'JSON'
        formats = ['JSON']
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
    javadoc.failOnError = false

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                groupId = groupId
                artifactId = artifactId
                version = project.property('version')

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', artifactDescription)
                    root.appendNode('name', artifactId)
                    root.appendNode('url', 'https://github.com/bancolombia/secrets-manager')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    if (project.hasProperty('signing.keyId')) {
        signing {
            sign publishing.publications.mavenJava
        }
    }
}

tasks.register('generateMergedReport', JacocoReport) {
    dependsOn test
    dependsOn subprojects.test
    dependsOn subprojects.javadoc
    dependsOn subprojects.jacocoTestReport
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.setRequired true
        csv.setRequired false
        html.setRequired true
    }
}

tasks.named('wrapper') {
    gradleVersion = '8.13'
}