allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'

        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

        testImplementation 'org.mockito:mockito-core:3.12.4'
        testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
        testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
        testImplementation 'junit:junit:4.13.2'
    }

    sonarqube {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.projectKey", "bancolombia_secrets-manager"
            property "sonar.organization", "grupo-bancolombia"
            property "sonar.host.url", "https://sonarcloud.io/"
            property "sonar.sources", "src/main"
            property "sonar.test", "src/test"
            property "sonar.java.binaries", "build/classes"
            property "sonar.junit.reportPaths", "build/test-results/test"
            property "sonar.java-coveragePlugin", "jacoco"
            property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/generateMergedReport/generateMergedReport.xml"
            property "sonar.exclusions", ".github/**"
        }
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    ext {
        pomConfig = {
            licenses {
                license {
                    name "MIT License"
                    url "https://opensource.org/licenses/mit-license.php"
                    distribution "repo"
                }
            }
            developers {
                developer {
                    id "dericop"
                    name "Daniel Rico Posada"
                    email "danielrico.posada@gmail.com"
                }
                developer {
                    id "alejobtc"
                    name "Alejandro Betancur Barrientos"
                    email "alejobtc@gmail.com"
                }
                developer {
                    id "andmagom"
                    name "Andres Mauricio Gomez P"
                    email "andmagom@outlook.com"
                }
            }

            scm {
                url "git@github.com:bancolombia/secrets-manager.git"
            }
        }
        groupId = 'com.github.bancolombia'
    }

    group groupId

    sourceCompatibility = JavaVersion.VERSION_1_8

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
    javadoc.failOnError = false

    repositories {
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                groupId = groupId
                artifactId = artifactId
                version = project.property('version')

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', artifactDescription)
                    root.appendNode('name', artifactId)
                    root.appendNode('url', 'https://github.com/bancolombia/secrets-manager')
                    root.children().last() + pomConfig
                }
            }
        }
        repositories {
            maven {
                name = 'OSSRH'
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = System.getenv('MAVEN_USERNAME')
                    password = System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    if (project.hasProperty('signing.keyId')) {
        signing {
            sign publishing.publications.mavenJava
        }
    }
}

task generateMergedReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

tasks.named('wrapper') {
    gradleVersion = '7.4.2'
}
