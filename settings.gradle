/*
 * This settings file was generated by the Gradle 'init' task.
 *
 * The settings file is used to specify which projects to include in your build.
 * In a single project build this file can be empty or even removed.
 *
 * Detailed information about configuring a multi-project build in Gradle can be found
 * in the user guide at https://docs.gradle.org/4.3/userguide/multi_project_builds.html
 */

/*
// To declare projects as part of a multi-project build use the 'include' method
include 'shared'
include 'api'
include 'services:webservice'
*/

rootProject.name = 'SecretsManager'

FileTree buildFiles = fileTree(rootDir) {
    List excludes = gradle.startParameter.projectProperties.get("excludeProjects")?.split(",")
    include '**/*.gradle'
    exclude 'main.gradle', 'build', '**/gradle', '**/settings.gradle', 'buildSrc', '/build.gradle', '.*', 'out'
    exclude '**/grails3'
    if(excludes) {
        exclude excludes
    }
}

String rootDirPath = rootDir.absolutePath + File.separator
buildFiles.each { File buildFile ->

    boolean isDefaultName = 'build.gradle'.equals(buildFile.name)
    if(isDefaultName) {
        String buildFilePath = buildFile.parentFile.absolutePath
        String projectPath = buildFilePath.replace(rootDirPath, '').replace(File.separator, ':')
        include projectPath
    } else {
        String projectName = buildFile.name.replace('.gradle', '');
        String projectPath = ':' + projectName;
        include projectPath
        def project = findProject("${projectPath}")
        project.name = projectName
        project.projectDir = buildFile.parentFile
        project.buildFileName = buildFile.name
    }
}

